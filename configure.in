
AC_INIT(ming, 0.4.0-beta)

dnl Tell configure to look in the config subdirectory
dnl for config.guess, config.sub, etc
AC_CONFIG_AUX_DIR(config)

AM_INIT_AUTOMAKE

dnl -- Release version 
MAJOR_VERSION=0
MINOR_VERSION=4
MICRO_VERSION=0-beta

MING_VERSION=${MAJOR_VERSION}.${MINOR_VERSION}.${MICRO_VERSION}

dnl -- Version info for libtool
INTERFACE_CURRENT=4
INTERFACE_REVISION=0
INTERFACE_AGE=4

AC_SUBST(INTERFACE_CURRENT)
AC_SUBST(INTERFACE_REVISION)
AC_SUBST(INTERFACE_AGE)

dnl GNU recommended way to determine host variables (OS, etc)
AC_CANONICAL_HOST

AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL

AC_PATH_PROG(PYTHON, python)
dnl AM_CONDITIONAL(USE_PYTHON, test x"$PYTHON" != x)
AM_CONDITIONAL(BUILD_PYTHON_EXTENSION, false)

AC_PATH_PROG(PERL, perl)
dnl AM_CONDITIONAL(USE_PERL, test x"$PERL" != x)
AM_CONDITIONAL(HAVE_PERL, test x"$PERL" != x)
AM_CONDITIONAL(BUILD_PERL_EXTENSION, false)

AC_PATH_PROG(PHP, php)
dnl AM_CONDITIONAL(USE_PHP, test x"$PHP" != x)
AM_CONDITIONAL(BUILD_PHP_EXTENSION, false)

AC_PROG_YACC
AM_PROG_LEX
AC_PROG_LIBTOOL

dnl Check if the X libraries are installed (needed for libungif on at least Solaris)
AC_CHECK_LIB(X11, XGetImage, XLIB="-lX11", XLIB="")

dnl Check for the gif or ungif libraries
AC_CHECK_LIB(gif, PrintGifError, GIFLIB="-lgif", GIFLIB="")
dnl MinGW check for libungif
AC_CHECK_LIB(ungif, DGifOpen, GIFLIB="-lungif")
dnl Solaris needs -lX11 on the linker line for ungif to work
AC_CHECK_LIB(ungif, PrintGifError, GIFLIB="-lungif",, "-lX11")

dnl Check for the png library
dnl Solaris needs -lm on the linker line, and other platforms aren't bothered having it there. :)
AC_CHECK_LIB(png, png_read_image, PNGLIB="-lpng", PNGLIB="", "-lm")

dnl Check for the zlib library
AC_CHECK_LIB(z, compress2,
	ZLIB="-lz",
	dnl Windows has the zlib library, but its named zdll instead
	AC_CHECK_LIB(zdll, compress2, ZLIB="-lzdll", ZLIB="")
)
AC_CHECK_HEADERS([zlib.h])

dnl Check if the vasprintf function exists on this platform
dnl (It's not present on MinGW and Solaris 10 at the time of this coding)
dnl Also check for the presence of the mkstemp function (it's not on MinGW)
AC_CHECK_FUNCS(vasprintf mkstemp)

dnl Check for various getopt functions
AC_CHECK_FUNCS(getopt getopt_long)

AC_CHECK_FUNC(sin, MATHLIB="", [
        AC_CHECK_LIB(m, sin, MATHLIB="-lm", [
		AC_ERROR([I can't find sin() function !!!])]
	)]
)

AC_DEFINE(TRACK_ALLOCS, 1, [Define this if you want Ming to track all objects allocations. Ming will mantain a doubly linked list of allocated objects, call Ming_collectGarbage() to get rid of them all])

AM_CONDITIONAL(USE_ZLIB, test x${ZLIB} != x)
if test -n "${ZLIB}"; then
	AC_DEFINE([USE_ZLIB], [1], [Use zlib])
fi

AM_CONDITIONAL(USE_GIF, test x${GIFLIB} != x)
if test -n "${GIFLIB}"; then
	AC_DEFINE(USE_GIF, [1], [Use gif library])
fi

AM_CONDITIONAL(USE_PNG, test x${PNGLIB} != x)
if test -n "${PNGLIB}"; then
 	AC_DEFINE(USE_PNG, [1], [Use png library])
fi

CFLAGS="$CFLAGS -Wall"
MACHINE=`uname -m`
if test "$MACHINE" = "x86_64"; then
	CFLAGS="$CFLAGS -fPIC"
fi

AC_SUBST(SHLIBLDFLAGS)
AC_SUBST(SHLIBEXT)
AC_SUBST(SHCFLAGS)
AC_SUBST(GIFLIB)
AC_SUBST(PNGLIB)
AC_SUBST(MATHLIB)
AC_SUBST(XLIB)
AC_SUBST(ZLIB)
AC_SUBST(SHORT_SHLIBEXT)
AC_SUBST(NOVAR_SHLIBEXT)
AC_SUBST(MAJOR_VERSION)
AC_SUBST(MINOR_VERSION)
AC_SUBST(MICRO_VERSION)
AC_SUBST(MING_VERSION)

AC_CONFIG_HEADER(src/ming_config.h)

dnl AC_OUTPUT(Makefile.config src/ming.h util/ming-config)
AC_OUTPUT([
	docs/Makefile
	docs/man/Makefile
	Makefile 
	src/actioncompiler/Makefile
	src/blocks/Makefile
	src/Makefile
	src/ming.h
	test/Makefile
	test/Movie/Dimension/Makefile
	test/Movie/Makefile
	test/Movie/new/Makefile
	test/Movie/NumFrames/Makefile
	test/Movie/Rate/Makefile
	macros/Makefile
	util/Makefile
	util/ming-config
	test/dotests
])
