AC_INIT(ming.i)

MAJOR_VERSION=0
MINOR_VERSION=3
MICRO_VERSION=0beta1

AC_PROG_CC
AC_PROG_CXX
AC_PATH_PROG(PYTHON, python)

AC_CHECK_LIB(gif, PrintGifError,
	GIFLIB="-lgif", [
	AC_CHECK_LIB(ungif, PrintGifError,
		GIFLIB="-lungif",
		GIFLIB="")]
)

AC_CHECK_LIB(png, png_read_image, PNGLIB="-lpng", PNGLIB="")

AC_CHECK_LIB(z, compress2, ZLIB="-lz", ZLIB="")

AC_CHECK_FUNC(sin, MATHLIB="", [
        AC_CHECK_LIB(m, sin, MATHLIB="-lm", [
		AC_ERROR([I can't find sin() function !!!])]
	)]
)

EXTRA_OBJS=
EXTRA_BINS=

if test -n "${GIFLIB}"; then
	EXTRA_OBJS="$EXTRA_OBJS gifdbl.o"
	EXTRA_BINS="$EXTRA_BINS gif2dbl gif2mask"
	AC_DEFINE(USE_GIF)
	AC_DEFINE(USE_ZLIB)
fi

if test -n "${PNGLIB}"; then
	EXTRA_OBJS="$EXTRA_OBJS pngdbl.o"
	EXTRA_BINS="$EXTRA_BINS png2dbl png2swf"
	AC_DEFINE(USE_PNG)
	AC_DEFINE(USE_ZLIB)
fi

 
OS=`uname -s`
case "$OS" in
"Darwin")
	SHLIBEXT=".\$(MING_VER).dylib"
	SHORT_SHLIBEXT=".\$(MAJOR_VERSION).dylib"
	NOVAR_SHLIBEXT=".dylib"
	SHLIBLDFLAGS="-dynamiclib \
		-compatibility_version \
		\`expr 1 + \"\$(MAJOR_VERSION)\"\`.\$(MINOR_VERSION) \
		-current_version \
		\`expr 1 + \"\$(MAJOR_VERSION)\"\`.\$(MINOR_VERSION) \
		-install_name \$(libdir)/\$(SHAREDLIB)"
	CFLAGS="$CFLAGS -fno-common"
	;;
"Linux")
	SHLIBLDFLAGS="-shared -Wl,-soname,libming.so.\$(MAJOR_VERSION)"
	SHLIBEXT=".so.\$(MING_VER)"
	SHORT_SHLIBEXT=".so.\$(MAJOR_VERSION)"
	NOVAR_SHLIBEXT=".so"
	SHCFLAGS="-fPIC"
	;;
*)
	SHLIBLDFLAGS="-shared"
	SHLIBEXT=".so.\$(MING_VER)"
	SHORT_SHLIBEXT=".so.\$(MAJOR_VERSION)"
	NOVAR_SHLIBEXT=".so"
esac

MACHINE=`uname -m`
if test "$MACHINE" = "x86_64"; then
	CFLAGS="$CFLAGS -fPIC"
fi

AC_SUBST(SHLIBLDFLAGS)
AC_SUBST(SHLIBEXT)
AC_SUBST(SHCFLAGS)
AC_SUBST(EXTRA_OBJS)
AC_SUBST(EXTRA_BINS)
AC_SUBST(GIFLIB)
AC_SUBST(PNGLIB)
AC_SUBST(MATHLIB)
AC_SUBST(ZLIB)
AC_SUBST(SHORT_SHLIBEXT)
AC_SUBST(NOVAR_SHLIBEXT)
AC_SUBST(MAJOR_VERSION)
AC_SUBST(MINOR_VERSION)
AC_SUBST(MICRO_VERSION)

AC_CONFIG_HEADER(src/ming_config.h)

AC_OUTPUT(Makefile.config src/ming.h)
